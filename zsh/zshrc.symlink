# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# all of our zsh files
typeset -U config_files
config_files=($(find $ZSH/**/*.zsh | grep -v /nvim.symlink/ | grep -v "/lib/*/"))

source <(antibody init)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load antibody plugins
source ~/.zsh_plugins.sh

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

autoload -Uz compinit
typeset -i updated_at=$(date +'%j' -r ~/.zcompdump 2>/dev/null || stat -f '%Sm' -t '%j' ~/.zcompdump 2>/dev/null)
if [ $(date +'%j') != $updated_at ]; then
  compinit -i
else
  compinit -C -i
fi

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files updated_at

[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh

eval "$(nodenv init -)"

# In case of v6. Enable this
# source /Users/sedivy/Documents/scratch/invision/InVision_Docker/scripts-native/invision.sh

source /Users/sedivy/Documents/scratch/invision/InVision_Docker/scripts-native/invision.sh
